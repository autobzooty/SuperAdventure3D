///////////////////////////////////////////////////////////////////////////////
///           |
/// AUTHORS   | Doug Zwick
/// COPYRIGHT | (C) 2018 Milky Way Games
///           |
///////////////////////////////////////////////////////////////////////////////

[RunInEditor]
class DebugDrawOnFrameUpdate : ZilchComponent
{
  [Property]
  var DrawInEditor : Boolean = true;
  [Property]
  var DrawInGame : Boolean = true;
  [Property]
  var ChildrenToo : Boolean = true;
  
  
  //***************************************************************************
  function Initialize(init : CogInitializer)
  {
    Zero.Connect(this.Space, Events.FrameUpdate, this.OnFrameUpdate);
  }
  
  
  //***************************************************************************
  function OnFrameUpdate(event : UpdateEvent)
  {
    if (this.DrawInEditor && this.Space.IsEditorMode || this.DrawInGame && !this.Space.IsEditorMode)
      this.Draw();
  }
  
  
  //***************************************************************************
  function Draw()
  {
    if (this.ChildrenToo)
      this.DrawRecursive(this.Owner);
    else
      this.Owner.DebugDraw();
  }
  
  
  //***************************************************************************
  function DrawRecursive(cog : Cog)
  {
    cog.DebugDraw();
    
    foreach (var child in cog.Children)
      this.DrawRecursive(child);
  }
}
